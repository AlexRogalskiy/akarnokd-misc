buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.9.0'
        classpath "me.champeau.gradle:jmh-gradle-plugin:0.2.0"
    }
}

apply plugin: 'java'

apply plugin: 'eclipse'

apply plugin: "me.champeau.gradle.jmh"

targetCompatibility = '1.8'
sourceCompatibility = '1.8'

group = "com.github.akarnokd" 
ext.githubProjectName = 'akarnokd-misc'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

if (!hasProperty('mainClass')) {
    ext.mainClass = ''
}



apply from: file('gradle/maven.gradle')

repositories {
    mavenCentral()
	
    maven { url 'https://oss.jfrog.org/libs-snapshot' }
}

apply plugin: 'maven'
apply plugin: 'osgi'

configurations {
  compile1
  compile2
}

dependencies {
    compile 'org.reactivestreams:reactive-streams:1.0.0'
    compile1 'io.reactivex:rxjava:2.0.0-DP0-SNAPSHOT'
    compile2 'io.reactivex:rxjava:1.0.14'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
}

task libs(type: Sync) {
  from configurations.compile1
  from configurations.compile2
  into "$buildDir/libs"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
	archives jar
    archives sourcesJar
    archives javadocJar
}

jar {
    manifest {
        name = 'akarnokd-misc'
        instruction 'Bundle-Vendor', 'akarnokd'
        instruction 'Bundle-DocURL', 'https://github.com/akarnokd/akarnokd-misc'
        instruction 'Import-Package', '!org.junit,!junit.framework,!org.mockito.*,*'
        instruction 'Eclipse-ExtensibleAPI', 'true'
    }
}

apply plugin: 'license'

apply from: file('gradle/license.gradle')

jmh {
    jmhVersion = '1.11.1'
    humanOutputFile = null
    if (project.hasProperty('jmh')) {
        include = ".*" + project.jmh + ".*"
    } else {
        include = ".*"
    }
}

plugins.withType(EclipsePlugin) {
    project.eclipse.classpath.plusConfigurations += [ configurations.jmh ]
    project.eclipse.classpath.plusConfigurations += [ configurations.compile1 ]
    project.eclipse.classpath.plusConfigurations += [ configurations.compile2 ]
}

javadoc {
    failOnError = false
}

test {
    maxHeapSize = "2g"
    testLogging  {
        events "started", "failed" // "skipped", "passed"
    //    showStandardStreams = true
    }
}